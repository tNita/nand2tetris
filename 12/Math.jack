// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int i, value;

        let n = 16;
        let powersOfTwo = Array.new(n);

        let i = 0;
        let value = 1;
        while (i < n) {
            let powersOfTwo[i] = value;
            let value = value + value;
            let i = i + 1;
        }

        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i;

        let sum = 0;
        let shiftedX = x;
        let i = 0;

        while (i < n) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;
    }

    function boolean bit(int value, int idx) {
        return ~((value & powersOfTwo[idx]) = 0);
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int absX, absY, q, doubleY, doubleQ, remainder, i, result;

        let absX = Math.abs(x);
        let absY = Math.abs(y);
        if (((x < 0) & (y > 0)) | ((x > 0) & (y < 0)) ) {
            return -Math.unsignedDivide(absX, absY);
        }
        return Math.unsignedDivide(absX, absY);
    }

    function int unsignedDivide(int x, int y) {
        var int q, doubleQ;
        if (y = 0) {
            // TODO: call Sys.error
            return 0;
        }

        // if y < 0, y is overflowed
        if ((y > x) | (y < 0)) {
            return 0;
        }
        let q = Math.unsignedDivide(x, y + y);

        let doubleQ = q + q;
        if (x - (doubleQ *  y) < y) {
            return doubleQ;
        } else {
            return doubleQ + 1;
        }
    }


    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, z, doubleZ;
        if (x < 0) {
            // TODO: call Sys.error
            return 0;
        }
        let j = (n / 2) - 1;
        let y = 0;
        while (j > -1) {
            let z = y + powersOfTwo[j];
            let doubleZ = z * z;
            if ((~(doubleZ > x)) & (doubleZ > 0)) {
                let y = z;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if(a > b){
			return a;
		} else {
			return b;
		}
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if(a > b){
			return b;
		} else {
			return a;
		}
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0){
            return -x;
        }
        return x;
    }
}
